plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'info.solidsoft.pitest' version '1.15.0'
    id "org.sonarqube" version "5.0.0.4638"
}

jacoco {
    toolVersion = '0.8.12'
}

pitest {
    pitestVersion = '1.15.0'
    junit5PluginVersion = '1.2.0'
    testPlugin = 'junit5'
    threads = 4
    outputFormats = ['HTML']

    targetClasses = ['com.korovko.arm_controller.service.*']
    targetTests = ['com.korovko.arm_controller.service.*']

    mutationThreshold = 0
    coverageThreshold = 0
}

group = 'com.korovko'
version = '0.0.1-SNAPSHOT'
description = 'ARM Controller'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'io.cucumber:cucumber-java:7.18.0'
    testImplementation "io.cucumber:cucumber-junit-platform-engine:7.18.0"
    testImplementation "org.junit.platform:junit-platform-suite"
    testImplementation "org.springframework:spring-test:6.1.0"
    testImplementation "com.code-intelligence:jazzer-junit:0.28.0"
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = false
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: [
                    '**/config/**',
                    '**/model/**'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: [
                    '**/config/**',
                    '**/model/**'
            ])
        }))
    }

    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "arm-controller"
        property "sonar.projectName", "ARM Controller"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.sourceEncoding", "UTF-8"
    }
}
